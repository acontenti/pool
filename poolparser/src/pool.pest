//
// Created by intellij-pest on 2020-07-02
// pool
// Author: Alessandro
//

pool = _{ SOI ~ statement* ~ EOI  }

statement = { expression? ~ ";" }

expression = _{ call }

call = { (invocation? ~ operator ~ invocation) | (invocation ~ operator? ~ invocation?) }

invocation = { access ~ args? }

access = { term ~ ("." ~ identifier)* }

term = { null | num | bool | string | fun | tuple | array | par | block | identifier }

par = { "(" ~ expression? ~ ")" }

block = { "{" ~ (statement)* ~ "}" }

tuple = { "(" ~ (expression ~ ",")+ ~ expression? ~ ")" }

array = { "[" ~ expression? ~ ("," + ~ expression)* ~ "]" }

fun = { params ~ ":" ~ block }

params = { "(" ~ identifier? ~ ("," ~ identifier)* ~ ")" }

args = { "(" ~ expression? ~ ("," ~ expression)* ~ ")" }

num = { decimal | integer }

bool = @{ "true" | "false" }

null = @{ "null" }

decimal = @{ "-"? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) }

integer = @{ "-"? ~ ASCII_DIGIT+ }

string = ${ "\"" ~ inner ~ "\"" }

inner = @{ char* }

char = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

identifier = @{ "$" ~ IDENT_PART+ }

operator = @{ OP_START ~ OP_PART* }

IDENT_PART = @{ ASCII_ALPHANUMERIC | "_" }

OP_START = @{ ASCII_ALPHA       | "_" | "!" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

OP_PART = @{ ASCII_ALPHANUMERIC | "_" | "!" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!NEWLINE ~ ANY)*) }