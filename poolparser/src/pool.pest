//
// Created by intellij-pest on 2020-07-02
// pool
// Author: Alessandro
//

pool = _{ SOI ~ statement* ~ EOI  }

statement = { expression? ~ ";" }

expression = _{ invocation | call | access | term }

call = { term ~ identifier ~ term }

invocation = { term ~ args }

term = { null | num | bool | string | fun | tuple | array | par | block | identifier }

access = { term ~ "." ~ identifier }

par = { "(" ~ expression? ~ ")" }

block = { "{" ~ (statement)* ~ "}" }

tuple = { "(" ~ (expression ~ ",")+ ~ expression? ~ ")" }

array = { "[" ~ expression? ~ ("," + ~ expression)* ~ "]" }

fun = { params ~ ":" ~ block }

params = { "(" ~ identifier? ~ ("," ~ identifier)* ~ ")" }

args = { "(" ~ expression? ~ ("," ~ expression)* ~ ")" }

num = { decimal | integer }

bool = @{ "true" | "false" }

null = @{ "null" }

decimal = @{ "-"? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) }

integer = @{ "-"? ~ ASCII_DIGIT+ }

string = ${ "\"" ~ inner ~ "\"" }

inner = @{ char* }

char = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

identifier = @{ IDENT_START ~ IDENT_PART* }

IDENT_START = @{ ASCII_ALPHA | "_" | "!" | "$" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

IDENT_PART = @{ ASCII_ALPHANUMERIC | "_" | "!" | "$" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }