//
// Created by intellij-pest on 2020-07-02
// pool
// Author: Alessandro
//

pool = _{ SOI ~ statement* ~ EOI  }

statement = { expression? ~ ";" }

expression = _{ call | term }

call = { term ~ identifier ~ term }

term = { null | num | bool | string | fun | tuple | array | par | block | variable }

variable = { identifier ~ ("." ~ identifier)? }

par = { "(" ~ expression? ~ ")" }

block = { "{" ~ (statement)* ~ "}" }

tuple = { "(" ~ (expression ~ ",")+ ~ expression? ~ ")" }

array = { "[" ~ expression? ~ ("," + ~ expression)* ~ "]" }

fun = { args ~ ":" ~ block }

args = { "(" ~ identifier? ~ ("," ~ identifier)* ~ ")" }

num = { decimal | integer }

bool = @{ "true" | "false" }

null = @{ "null" }

decimal = @{ "-"? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) }

integer = @{ "-"? ~ ASCII_DIGIT+ }

string = ${ "\"" ~ inner ~ "\"" }

inner = @{ char* }

char = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

identifier = @{ IDENT_START ~ IDENT_PART* }

IDENT_START = @{ ASCII_ALPHA | "_" | "!" | "$" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

IDENT_PART = @{ ASCII_ALPHANUMERIC | "_" | "!" | "$" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | ">" | "=" | "^" | "|" | "~" }

WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }